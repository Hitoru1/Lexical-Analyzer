1.   <program> ⇒ <group_part>

2.   <group_part> ⇒ <group_definitions> <group_part>
3.   <group_part> ⇒ <worldwide_part>

4.   <worldwide_part> ⇒ <global_variable_declarations> <worldwide_part>
5.   <worldwide_part> ⇒ <define_part>

6.   <define_part> ⇒ <function_definitions> <define_part>
7.   <define_part> ⇒ <start_block>

8.   <start_block> ⇒ start { <statements> } finish
9.   <group_definitions> ⇒ group IDENTIFIER { <group_body> }

10.  <group_body> ⇒ <group_member> <group_body_tail>
11.  <group_body> ⇒ λ

12.  <group_body_tail> ⇒ <group_member> <group_body_tail>
13.  <group_body_tail> ⇒ λ

14.  <group_member> ⇒ <datatype> IDENTIFIER ;

15.  <datatype> ⇒ num
16.  <datatype> ⇒ decimal
17.  <datatype> ⇒ bigdecimal
18.  <datatype> ⇒ bool
19.  <datatype> ⇒ text
20.  <datatype> ⇒ letter

21.  <global_variable_declarations> ⇒ worldwide <global_typed_decl>

22.  <global_typed_decl> ⇒ num IDENTIFIER = <stmt_value> ;
23.  <global_typed_decl> ⇒ decimal IDENTIFIER = <stmt_value> ;
24.  <global_typed_decl> ⇒ bigdecimal IDENTIFIER = <stmt_value> ;
25.  <global_typed_decl> ⇒ bool IDENTIFIER = <stmt_value> ;
26.  <global_typed_decl> ⇒ text IDENTIFIER = <stmt_value> ;
27.  <global_typed_decl> ⇒ letter IDENTIFIER = <stmt_value> ;

28.  <function_definitions> ⇒ define <return_type> IDENTIFIER ( <parameter_list> ) 
                              { <local_declarations> <statements> <optional_return> }

29.  <return_type> ⇒ num
30.  <return_type> ⇒ decimal
31.  <return_type> ⇒ bigdecimal
32.  <return_type> ⇒ bool
33.  <return_type> ⇒ text
34.  <return_type> ⇒ letter
35.  <return_type> ⇒ empty

36.  <parameter_list> ⇒ <parameter> <parameter_list_tail>
37.  <parameter_list> ⇒ λ

38.  <parameter_list_tail> ⇒ , <parameter> <parameter_list_tail>
39.  <parameter_list_tail> ⇒ λ

40.  <parameter> ⇒ <datatype> IDENTIFIER

41.  <optional_return> ⇒ give <return_tail>
42.  <optional_return> ⇒ λ

43.  <return_tail> ⇒ <stmt_value> ;
44.  <return_tail> ⇒ ;

45.  <local_declarations> ⇒ <declaration> <local_declarations>
46.  <local_declarations> ⇒ λ

47.  <declaration> ⇒ <local_declaration>
48.  <declaration> ⇒ <fixed_declaration>
49.  <declaration> ⇒ <list_declaration>

50.  <local_declaration> ⇒ IDENTIFIER IDENTIFIER ;
51.  <local_declaration> ⇒ num IDENTIFIER = <stmt_value> ;
52.  <local_declaration> ⇒ decimal IDENTIFIER = <stmt_value> ;
53.  <local_declaration> ⇒ bigdecimal IDENTIFIER = <stmt_value> ;
54.  <local_declaration> ⇒ bool IDENTIFIER = <stmt_value> ;
55.  <local_declaration> ⇒ text IDENTIFIER = <stmt_value> ;
56.  <local_declaration> ⇒ letter IDENTIFIER = <stmt_value> ;

57.  <fixed_declaration> ⇒ fixed <fixed_typed_decl>

58.  <fixed_typed_decl> ⇒ num IDENTIFIER = <stmt_value> ;
59.  <fixed_typed_decl> ⇒ decimal IDENTIFIER = <stmt_value> ;
60.  <fixed_typed_decl> ⇒ bigdecimal IDENTIFIER = <stmt_value> ;
61.  <fixed_typed_decl> ⇒ bool IDENTIFIER = <stmt_value> ;
62.  <fixed_typed_decl> ⇒ text IDENTIFIER = <stmt_value> ;
63.  <fixed_typed_decl> ⇒ letter IDENTIFIER = <stmt_value> ;

64.  <list_declaration> ⇒ list <list_typed_decl>

65.  <list_typed_decl> ⇒ num IDENTIFIER = <num_list> ;
66.  <list_typed_decl> ⇒ decimal IDENTIFIER = <num_list> ;
67.  <list_typed_decl> ⇒ bigdecimal IDENTIFIER = <num_list> ;
68.  <list_typed_decl> ⇒ bool IDENTIFIER = <bool_list> ;
69.  <list_typed_decl> ⇒ text IDENTIFIER = <expr_list> ;
70.  <list_typed_decl> ⇒ letter IDENTIFIER = <expr_list> ;

71.  <num_list> ⇒ <num_list_1d>
72.  <num_list> ⇒ <num_list_2d>

73.  <num_list_1d> ⇒ [ <num_list_elems> ]

74.  <num_list_elems> ⇒ <arg_value> <num_list_tail>
75.  <num_list_elems> ⇒ λ

76.  <num_list_tail> ⇒ , <arg_value> <num_list_tail>
77.  <num_list_tail> ⇒ λ

78.  <num_list_2d> ⇒ [ <num_list_rows> ]

79.  <num_list_rows> ⇒ <num_list_1d> <num_list_rows_tail>
80.  <num_list_rows> ⇒ λ

81.  <num_list_rows_tail> ⇒ , <num_list_1d> <num_list_rows_tail>
82.  <num_list_rows_tail> ⇒ λ

83.  <bool_list> ⇒ <bool_list_1d>
84.  <bool_list> ⇒ <bool_list_2d>

85.  <bool_list_1d> ⇒ [ <bool_list_elems> ]

86.  <bool_list_elems> ⇒ <arg_value> <bool_list_tail>
87.  <bool_list_elems> ⇒ λ

88.  <bool_list_tail> ⇒ , <arg_value> <bool_list_tail>
89.  <bool_list_tail> ⇒ λ

90.  <bool_list_2d> ⇒ [ <bool_list_rows> ]

91.  <bool_list_rows> ⇒ <bool_list_1d> <bool_list_rows_tail>
92.  <bool_list_rows> ⇒ λ

93.  <bool_list_rows_tail> ⇒ , <bool_list_1d> <bool_list_rows_tail>
94.  <bool_list_rows_tail> ⇒ λ

95.  <expr_list> ⇒ <expr_list_1d>
96.  <expr_list> ⇒ <expr_list_2d>

97.  <expr_list_1d> ⇒ [ <expr_list_elems> ]

98.  <expr_list_elems> ⇒ <arg_value> <expr_list_tail>
99.  <expr_list_elems> ⇒ λ

100. <expr_list_tail> ⇒ , <arg_value> <expr_list_tail>
101. <expr_list_tail> ⇒ λ

102. <expr_list_2d> ⇒ [ <expr_list_rows> ]

103. <expr_list_rows> ⇒ <expr_list_1d> <expr_list_rows_tail>
104. <expr_list_rows> ⇒ λ

105. <expr_list_rows_tail> ⇒ , <expr_list_1d> <expr_list_rows_tail>
106. <expr_list_rows_tail> ⇒ λ

107. <statements> ⇒ <statement> <statements>
108. <statements> ⇒ λ

109. <statement> ⇒ <control_statement>
110. <statement> ⇒ <assignment_statement>
111. <statement> ⇒ <function_call_statement>
112. <statement> ⇒ <declaration>
113. <statement> ⇒ <io_statement>

114. <assignment_statement> ⇒ <assignable> <assignment_tail>

115. <assignment_tail> ⇒ = <stmt_value> ;
116. <assignment_tail> ⇒ += <stmt_value> ;
117. <assignment_tail> ⇒ -= <stmt_value> ;
118. <assignment_tail> ⇒ *= <stmt_value> ;
119. <assignment_tail> ⇒ /= <stmt_value> ;
120. <assignment_tail> ⇒ % = <stmt_value> ;
121. <assignment_tail> ⇒ **= <stmt_value> ;
122. <assignment_tail> ⇒ ++ ;
123. <assignment_tail> ⇒ -- ;

124. <assignable> ⇒ IDENTIFIER <assignable_suffix>

125. <assignable_suffix> ⇒ [ <index_value> ] <assignable_2d>
126. <assignable_suffix> ⇒ . IDENTIFIER
127. <assignable_suffix> ⇒ λ

128. <assignable_2d> ⇒ [ <index_value> ]
129. <assignable_2d> ⇒ λ

130. <function_call_statement> ⇒ <function_call> ;

131. <function_call> ⇒ IDENTIFIER ( <arg_list> )

132. <arg_list> ⇒ <arg_value> <arg_list_tail>
133. <arg_list> ⇒ λ

134. <arg_list_tail> ⇒ , <arg_value> <arg_list_tail>
135. <arg_list_tail> ⇒ λ

136. <io_statement> ⇒ show ( <arg_list> ) ;
137. <io_statement> ⇒ read ( IDENTIFIER ) ;

138. <control_statement> ⇒ <check_structure>
139. <control_statement> ⇒ <select_statement>
140. <control_statement> ⇒ <iterative_statement>

141. <check_structure> ⇒ check ( <cond_value> ) { <statements> } <otherwise_chain>

142. <otherwise_chain> ⇒ otherwisecheck ( <cond_value> ) { <statements> } <otherwise_chain>
143. <otherwise_chain> ⇒ otherwise { <statements> }
144. <otherwise_chain> ⇒ λ

145. <select_statement> ⇒ select ( IDENTIFIER ) { <option_blocks> <optional_fallback> }

146. <option_blocks> ⇒ <option_block> <option_blocks>
147. <option_blocks> ⇒ λ

148. <option_block> ⇒ option <literal> : <statements> <control_flow> ;

149. <control_flow> ⇒ stop
150. <control_flow> ⇒ skip

151. <optional_fallback> ⇒ fallback : <statements>
152. <optional_fallback> ⇒ λ

153. <iterative_statement> ⇒ <each_loop>
154. <iterative_statement> ⇒ <during_loop>

155. <each_loop> ⇒ each IDENTIFIER from <from_primary> to <to_primary> <step_clause> { <statements> }

156. <step_clause> ⇒ step <step_primary>
157. <step_clause> ⇒ λ

158. <during_loop> ⇒ during ( <cond_value> ) { <statements> }

159. <literal> ⇒ NUM_LIT
160. <literal> ⇒ DECIMAL_LIT
161. <literal> ⇒ STRING_LIT
162. <literal> ⇒ CHAR_LIT
163. <literal> ⇒ Yes
164. <literal> ⇒ No

165. <stmt_value> ⇒ <stmt_or>

166. <stmt_or> ⇒ <stmt_and> <stmt_or_tail>
167. <stmt_or_tail> ⇒ || <stmt_and> <stmt_or_tail>
168. <stmt_or_tail> ⇒ λ

169. <stmt_and> ⇒ <stmt_eq> <stmt_and_tail>
170. <stmt_and_tail> ⇒ && <stmt_eq> <stmt_and_tail>
171. <stmt_and_tail> ⇒ λ

172. <stmt_eq> ⇒ <stmt_rel> <stmt_eq_tail>
173. <stmt_eq_tail> ⇒ == <stmt_rel> <stmt_eq_tail>
174. <stmt_eq_tail> ⇒ != <stmt_rel> <stmt_eq_tail>
175. <stmt_eq_tail> ⇒ λ

176. <stmt_rel> ⇒ <stmt_add> <stmt_rel_tail>
177. <stmt_rel_tail> ⇒ > <stmt_add>
178. <stmt_rel_tail> ⇒ < <stmt_add>
179. <stmt_rel_tail> ⇒ >= <stmt_add>
180. <stmt_rel_tail> ⇒ <= <stmt_add>
181. <stmt_rel_tail> ⇒ λ

182. <stmt_add> ⇒ <stmt_mult> <stmt_add_tail>
183. <stmt_add_tail> ⇒ + <stmt_mult> <stmt_add_tail>
184. <stmt_add_tail> ⇒ - <stmt_mult> <stmt_add_tail>
185. <stmt_add_tail> ⇒ λ

186. <stmt_mult> ⇒ <stmt_exp> <stmt_mult_tail>
187. <stmt_mult_tail> ⇒ * <stmt_exp> <stmt_mult_tail>
188. <stmt_mult_tail> ⇒ / <stmt_exp> <stmt_mult_tail>
189. <stmt_mult_tail> ⇒ % <stmt_exp> <stmt_mult_tail>
190. <stmt_mult_tail> ⇒ λ

191. <stmt_exp> ⇒ <stmt_unary> <stmt_exp_tail>
192. <stmt_exp_tail> ⇒ ** <stmt_exp>
193. <stmt_exp_tail> ⇒ λ

194. <stmt_unary> ⇒ - <stmt_post>
195. <stmt_unary> ⇒ ! <stmt_post>
196. <stmt_unary> ⇒ <stmt_post>

197. <stmt_post> ⇒ <stmt_prim>

198. <stmt_prim> ⇒ ( <stmt_value> )
199. <stmt_prim> ⇒ <literal>
200. <stmt_prim> ⇒ IDENTIFIER <stmt_id_suffix>
201. <stmt_prim> ⇒ <size_call>

202. <stmt_id_suffix> ⇒ ( <arg_list> )
203. <stmt_id_suffix> ⇒ [ <index_value> ] <stmt_var_2d>
204. <stmt_id_suffix> ⇒ . IDENTIFIER
205. <stmt_id_suffix> ⇒ λ

206. <stmt_var_2d> ⇒ [ <index_value> ]
207. <stmt_var_2d> ⇒ λ

208. <arg_value> ⇒ <arg_or>

209. <arg_or> ⇒ <arg_and> <arg_or_tail>
210. <arg_or_tail> ⇒ || <arg_and> <arg_or_tail>
211. <arg_or_tail> ⇒ λ

212. <arg_and> ⇒ <arg_eq> <arg_and_tail>
213. <arg_and_tail> ⇒ && <arg_eq> <arg_and_tail>
214. <arg_and_tail> ⇒ λ

215. <arg_eq> ⇒ <arg_rel> <arg_eq_tail>
216. <arg_eq_tail> ⇒ == <arg_rel> <arg_eq_tail>
217. <arg_eq_tail> ⇒ != <arg_rel> <arg_eq_tail>
218. <arg_eq_tail> ⇒ λ

219. <arg_rel> ⇒ <arg_add> <arg_rel_tail>
220. <arg_rel_tail> ⇒ > <arg_add>
221. <arg_rel_tail> ⇒ < <arg_add>
222. <arg_rel_tail> ⇒ >= <arg_add>
223. <arg_rel_tail> ⇒ <= <arg_add>
224. <arg_rel_tail> ⇒ λ

225. <arg_add> ⇒ <arg_mult> <arg_add_tail>
226. <arg_add_tail> ⇒ + <arg_mult> <arg_add_tail>
227. <arg_add_tail> ⇒ - <arg_mult> <arg_add_tail>
228. <arg_add_tail> ⇒ λ

229. <arg_mult> ⇒ <arg_exp> <arg_mult_tail>
230. <arg_mult_tail> ⇒ * <arg_exp> <arg_mult_tail>
231. <arg_mult_tail> ⇒ / <arg_exp> <arg_mult_tail>
232. <arg_mult_tail> ⇒ % <arg_exp> <arg_mult_tail>
233. <arg_mult_tail> ⇒ λ

234. <arg_exp> ⇒ <arg_unary> <arg_exp_tail>
235. <arg_exp_tail> ⇒ ** <arg_exp>
236. <arg_exp_tail> ⇒ λ

237. <arg_unary> ⇒ - <arg_post>
238. <arg_unary> ⇒ ! <arg_post>
239. <arg_unary> ⇒ <arg_post>

240. <arg_post> ⇒ <arg_prim>

241. <arg_prim> ⇒ ( <arg_value> )
242. <arg_prim> ⇒ <literal>
243. <arg_prim> ⇒ IDENTIFIER <arg_id_suffix>
244. <arg_prim> ⇒ <size_call>

245. <arg_id_suffix> ⇒ ( <arg_list> )
246. <arg_id_suffix> ⇒ [ <index_value> ] <arg_var_2d>
247. <arg_id_suffix> ⇒ . IDENTIFIER
248. <arg_id_suffix> ⇒ λ

249. <arg_var_2d> ⇒ [ <index_value> ]
250. <arg_var_2d> ⇒ λ

251. <cond_value> ⇒ <cond_or>

252. <cond_or> ⇒ <cond_and> <cond_or_tail>
253. <cond_or_tail> ⇒ || <cond_and> <cond_or_tail>
254. <cond_or_tail> ⇒ λ

255. <cond_and> ⇒ <cond_eq> <cond_and_tail>
256. <cond_and_tail> ⇒ && <cond_eq> <cond_and_tail>
257. <cond_and_tail> ⇒ λ

258. <cond_eq> ⇒ <cond_comp> <cond_eq_tail>
259. <cond_eq_tail> ⇒ == <cond_comp> <cond_eq_tail>
260. <cond_eq_tail> ⇒ != <cond_comp> <cond_eq_tail>
261. <cond_eq_tail> ⇒ λ

262. <cond_comp> ⇒ <cond_unary>

263. <cond_unary> ⇒ ! <cond_base>
264. <cond_unary> ⇒ <cond_base>

265. <cond_base> ⇒ Yes
266. <cond_base> ⇒ No
267. <cond_base> ⇒ ( <cond_inner_paren>
268. <cond_base> ⇒ <cond_arith_noparen> <cond_rel_tail>

269. <cond_paren_tail> ⇒ ) <cond_rel_tail>
270. <cond_paren_tail> ⇒ <cond_rel_tail_in_paren> <cond_and_tail_ip> <cond_or_tail_ip> )

271. <cond_rel_tail_in_paren> ⇒ > <cond_arith_no_rel>
272. <cond_rel_tail_in_paren> ⇒ < <cond_arith_no_rel>
273. <cond_rel_tail_in_paren> ⇒ >= <cond_arith_no_rel>
274. <cond_rel_tail_in_paren> ⇒ <= <cond_arith_no_rel>
275. <cond_rel_tail_in_paren> ⇒ == <cond_eq_rhs>
276. <cond_rel_tail_in_paren> ⇒ != <cond_eq_rhs>

276.a  <cond_inner_paren> ⇒ <cond_arith> <cond_paren_tail>
276.b  <cond_inner_paren> ⇒ ! <cond_comparison_ip> <cond_and_tail_ip> <cond_or_tail_ip> )
276.c  <cond_inner_paren> ⇒ Yes <cond_and_tail_ip> <cond_or_tail_ip> )
276.d  <cond_inner_paren> ⇒ No <cond_and_tail_ip> <cond_or_tail_ip> )

276.e  <cond_and_tail_ip> ⇒ && <cond_comparison_ip> <cond_and_tail_ip>
276.f  <cond_and_tail_ip> ⇒ λ

276.g  <cond_or_tail_ip> ⇒ || <cond_and_ip> <cond_or_tail_ip>
276.h  <cond_or_tail_ip> ⇒ λ

276.i  <cond_and_ip> ⇒ <cond_comparison_ip> <cond_and_tail_ip>

276.j  <cond_comparison_ip> ⇒ <cond_arith_noparen> <cond_rel_tail_in_paren>
276.k  <cond_comparison_ip> ⇒ ( <cond_arith> <cond_paren_tail_ip>
276.l  <cond_comparison_ip> ⇒ ! <cond_comparison_ip>
276.m  <cond_comparison_ip> ⇒ Yes
276.n  <cond_comparison_ip> ⇒ No

276.o  <cond_paren_tail_ip> ⇒ ) <cond_rel_tail_in_paren>
276.p  <cond_paren_tail_ip> ⇒ <cond_rel_tail_in_paren> <cond_and_tail_ip> <cond_or_tail_ip> )

277. <cond_rel_tail> ⇒ > <cond_arith_no_rel>
278. <cond_rel_tail> ⇒ < <cond_arith_no_rel>
279. <cond_rel_tail> ⇒ >= <cond_arith_no_rel>
280. <cond_rel_tail> ⇒ <= <cond_arith_no_rel>
281. <cond_rel_tail> ⇒ == <cond_eq_rhs>
282. <cond_rel_tail> ⇒ != <cond_eq_rhs>

283. <cond_eq_rhs> ⇒ Yes
284. <cond_eq_rhs> ⇒ No
285. <cond_eq_rhs> ⇒ <cond_arith_no_rel>

286. <cond_arith_noparen> ⇒ <cond_add_noparen>

287. <cond_add_noparen> ⇒ <cond_mult_noparen> <cond_add_tail>

288. <cond_mult_noparen> ⇒ <cond_exp_noparen> <cond_mult_tail>

289. <cond_exp_noparen> ⇒ <cond_unary_noparen> <cond_exp_tail>

290. <cond_unary_noparen> ⇒ - <cond_post>
291. <cond_unary_noparen> ⇒ <cond_prim_noparen>

292. <cond_prim_noparen> ⇒ NUM_LIT
293. <cond_prim_noparen> ⇒ DECIMAL_LIT
294. <cond_prim_noparen> ⇒ IDENTIFIER <cond_id_suffix>
295. <cond_prim_noparen> ⇒ <size_call>

296. <cond_arith> ⇒ <cond_add>

297. <cond_add> ⇒ <cond_mult> <cond_add_tail>
298. <cond_add_tail> ⇒ + <cond_mult> <cond_add_tail>
299. <cond_add_tail> ⇒ - <cond_mult> <cond_add_tail>
300. <cond_add_tail> ⇒ λ

301. <cond_mult> ⇒ <cond_exp> <cond_mult_tail>
302. <cond_mult_tail> ⇒ * <cond_exp> <cond_mult_tail>
303. <cond_mult_tail> ⇒ / <cond_exp> <cond_mult_tail>
304. <cond_mult_tail> ⇒ % <cond_exp> <cond_mult_tail>
305. <cond_mult_tail> ⇒ λ

306. <cond_exp> ⇒ <cond_arith_unary> <cond_exp_tail>
307. <cond_exp_tail> ⇒ ** <cond_exp>
308. <cond_exp_tail> ⇒ λ

309. <cond_arith_unary> ⇒ - <cond_post>
310. <cond_arith_unary> ⇒ <cond_post>

311. <cond_post> ⇒ <cond_prim>

312. <cond_prim> ⇒ ( <cond_arith> )
313. <cond_prim> ⇒ NUM_LIT
314. <cond_prim> ⇒ DECIMAL_LIT
315. <cond_prim> ⇒ IDENTIFIER <cond_id_suffix>
316. <cond_prim> ⇒ <size_call>

317. <cond_id_suffix> ⇒ ( <arg_list> )
318. <cond_id_suffix> ⇒ [ <index_value> ] <cond_var_2d>
319. <cond_id_suffix> ⇒ . IDENTIFIER
320. <cond_id_suffix> ⇒ λ

321. <cond_var_2d> ⇒ [ <index_value> ]
322. <cond_var_2d> ⇒ λ

323. <cond_arith_no_rel> ⇒ <cond_add_no_rel>

324. <cond_add_no_rel> ⇒ <cond_mult_no_rel> <cond_add_tail_no_rel>
325. <cond_add_tail_no_rel> ⇒ + <cond_mult_no_rel> <cond_add_tail_no_rel>
326. <cond_add_tail_no_rel> ⇒ - <cond_mult_no_rel> <cond_add_tail_no_rel>
327. <cond_add_tail_no_rel> ⇒ λ

328. <cond_mult_no_rel> ⇒ <cond_exp_no_rel> <cond_mult_tail_no_rel>
329. <cond_mult_tail_no_rel> ⇒ * <cond_exp_no_rel> <cond_mult_tail_no_rel>
330. <cond_mult_tail_no_rel> ⇒ / <cond_exp_no_rel> <cond_mult_tail_no_rel>
331. <cond_mult_tail_no_rel> ⇒ % <cond_exp_no_rel> <cond_mult_tail_no_rel>
332. <cond_mult_tail_no_rel> ⇒ λ

333. <cond_exp_no_rel> ⇒ <cond_arith_unary_no_rel> <cond_exp_tail_no_rel>
334. <cond_exp_tail_no_rel> ⇒ ** <cond_exp_no_rel>
335. <cond_exp_tail_no_rel> ⇒ λ

336. <cond_arith_unary_no_rel> ⇒ - <cond_post_no_rel>
337. <cond_arith_unary_no_rel> ⇒ <cond_post_no_rel>

338. <cond_post_no_rel> ⇒ <cond_prim_no_rel>

339. <cond_prim_no_rel> ⇒ ( <cond_arith_no_rel> )
340. <cond_prim_no_rel> ⇒ NUM_LIT
341. <cond_prim_no_rel> ⇒ DECIMAL_LIT
342. <cond_prim_no_rel> ⇒ IDENTIFIER <cond_id_suffix_no_rel>
343. <cond_prim_no_rel> ⇒ <size_call>

344. <cond_id_suffix_no_rel> ⇒ ( <arg_list> )
345. <cond_id_suffix_no_rel> ⇒ [ <index_value> ] <cond_var_2d_no_rel>
346. <cond_id_suffix_no_rel> ⇒ . IDENTIFIER
347. <cond_id_suffix_no_rel> ⇒ λ

348. <cond_var_2d_no_rel> ⇒ [ <index_value> ]
349. <cond_var_2d_no_rel> ⇒ λ

350. <index_value> ⇒ <index_add>

351. <index_add> ⇒ <index_mult> <index_add_tail>
352. <index_add_tail> ⇒ + <index_mult> <index_add_tail>
353. <index_add_tail> ⇒ - <index_mult> <index_add_tail>
354. <index_add_tail> ⇒ λ

355. <index_mult> ⇒ <index_exp> <index_mult_tail>
356. <index_mult_tail> ⇒ * <index_exp> <index_mult_tail>
357. <index_mult_tail> ⇒ / <index_exp> <index_mult_tail>
358. <index_mult_tail> ⇒ % <index_exp> <index_mult_tail>
359. <index_mult_tail> ⇒ λ

360. <index_exp> ⇒ <index_unary> <index_exp_tail>
361. <index_exp_tail> ⇒ ** <index_exp>
362. <index_exp_tail> ⇒ λ

363. <index_unary> ⇒ - <index_post>
364. <index_unary> ⇒ <index_post>

365. <index_post> ⇒ <index_prim>

366. <index_prim> ⇒ ( <index_value> )
367. <index_prim> ⇒ NUM_LIT
368. <index_prim> ⇒ DECIMAL_LIT
369. <index_prim> ⇒ IDENTIFIER <index_id_suffix>
370. <index_prim> ⇒ <size_call>

371. <index_id_suffix> ⇒ ( <arg_list> )
372. <index_id_suffix> ⇒ [ <index_value> ] <index_var_2d>
373. <index_id_suffix> ⇒ . IDENTIFIER
374. <index_id_suffix> ⇒ λ

375. <index_var_2d> ⇒ [ <index_value> ]
376. <index_var_2d> ⇒ λ

377. <from_primary> ⇒ NUM_LIT
378. <from_primary> ⇒ DECIMAL_LIT
379. <from_primary> ⇒ IDENTIFIER <from_id_suffix>
380. <from_primary> ⇒ <size_call>

381. <from_id_suffix> ⇒ ( <arg_list> )
382. <from_id_suffix> ⇒ [ <index_value> ] <from_var_2d>
383. <from_id_suffix> ⇒ . IDENTIFIER
384. <from_id_suffix> ⇒ λ

385. <from_var_2d> ⇒ [ <index_value> ]
386. <from_var_2d> ⇒ λ

387. <to_primary> ⇒ NUM_LIT
388. <to_primary> ⇒ DECIMAL_LIT
389. <to_primary> ⇒ IDENTIFIER <to_id_suffix>
390. <to_primary> ⇒ <size_call>

391. <to_id_suffix> ⇒ ( <arg_list> )
392. <to_id_suffix> ⇒ [ <index_value> ] <to_var_2d>
393. <to_id_suffix> ⇒ . IDENTIFIER
394. <to_id_suffix> ⇒ λ

395. <to_var_2d> ⇒ [ <index_value> ]
396. <to_var_2d> ⇒ λ

397. <step_primary> ⇒ NUM_LIT
398. <step_primary> ⇒ DECIMAL_LIT
399. <step_primary> ⇒ IDENTIFIER <step_id_suffix>
400. <step_primary> ⇒ <size_call>

401. <step_id_suffix> ⇒ ( <arg_list> )
402. <step_id_suffix> ⇒ [ <index_value> ] <step_var_2d>
403. <step_id_suffix> ⇒ . IDENTIFIER
404. <step_id_suffix> ⇒ λ

405. <step_var_2d> ⇒ [ <index_value> ]
406. <step_var_2d> ⇒ λ

407. <size_call> ⇒ size ( IDENTIFIER <size_second_arg> )

408. <size_second_arg> ⇒ , NUM_LIT
409. <size_second_arg> ⇒ λ


